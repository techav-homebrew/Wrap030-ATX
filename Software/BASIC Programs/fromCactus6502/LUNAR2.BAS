20 PRINT "  ***      L U N A R      ***"
30 PRINT "        FOR THE CACTUS"
110 PRINT " "
120 PRINT " GROUND CONTROL CALLING LUNAR MODULE....."
130 PRINT " ON-BOARD AND GROUND COMPUTERS HAVE"
140 PRINT " SIMULTANEOUSLY MALFUNCTIONED."
150 PRINT " MANUAL CONTROL IS NECESSARY."
160 PRINT " CAPSULE WEIGHT 32,500 LBS."
170 PRINT "    AVAILABLE FUEL 16,500  LBS."
180 PRINT " ESTIMATED FREE FALL IMPACT TIME 120 SECONDS"
190 PRINT 
200 PRINT " TYPE IN ANY NUMBER TO SEE MORE";
210 INPUT I9
220 PRINT 
230 PRINT " SET RETRO ROCKET BURN RATE EVERY 10 SECONDS"
240 PRINT " TO ANY VALUE  BETWEEN 0 LBS/SEC (FREE FALL)"
250 PRINT " AND  200 LBS/SEC (STRONG BRAKING)"
260 PRINT 
270 PRINT "GOOD LUCK   !!!"
280 PRINT 
290 PRINT "SEC","MI + FT","MPH","LB FUEL","BURN RATE"
300 PRINT 
305 L=0
310 A=120
320 V=1
330 M=33000
340 N=16500
350 G=.001
360 Z=1.8
370 PRINT L,INT(A);INT(5280*(A-INT(A))),3600*V,M-N;
380 INPUT K
385 PRINT 
390 T=10
400 IF M-N<.001 THEN 510
410 IF T<.001 THEN 370
420 S=T
430 IF M>=N+S*K THEN 450
440 S=(M-N)/K
450 GOSUB 890
460 IF I<=0 THEN 750
470 IF V<=0 THEN 490
480 IF J<0 THEN 810
490 GOSUB 690
500 GOTO 400
510 PRINT "RAN OUT OF FUEL AT ";L;" SEC"
520 S=(-V+SQR(V*V+2*A*G))/G
530 V=V+G*S
540 L=INT(((L+S)*100)/100)
550 W=INT(3600*V)
560 PRINT "ON THE MOON AT ";L;" SEC - IMPACT VELOCITY ";W;" MPH"
570 IF W>1.2 THEN 600
580 PRINT "PERFECT LANDING!  (LUCKY)"
590 GOTO 930
600 IF W>10 THEN 630
610 PRINT "GOOD LANDING  (COULD BE BETTER)"
620 GOTO 930
630 IF W>60 THEN 660
640 PRINT "ARRIVES.  HOPE YOUR OXYGEN HOLDS OUT!"
650 GOTO 930
660 PRINT " SORRY...,  THERE WERE NO SURVIVORS"
670 PRINT " ------- YOU BLEW IT!"
680 GOTO 930
690 L=L+S
700 T=T-S
710 M=M-S*K
720 A=I
730 V=J
740 RETURN 
750 IF S<.005 THEN 550
760 D=V+SQR(V*V+2*A*(G-Z*K/M))
770 S=2*A/D
780 GOSUB 890
790 GOSUB 690
800 GOTO 750
810 W=(1-M*G/(Z*K))/2
820 S=M*V/(Z*K*(W+SQR(W*W+V/Z)))+.05
830 GOSUB 890
840 IF I<=0 THEN 750
850 GOSUB 690
860 IF J>0 THEN 400
870 IF V>0 THEN 810
880 GOTO 400
890 Q=S*K/M
900 J=V+G*S-Z*Q*(1+Q*(.5+Q*(1/3+Q*(.25+Q/5))))
910 I=A-G*S*S/2-V*S+Z*S*Q*(.5+Q*(1/6+Q*(1/12+Q/20)))
920 RETURN 
930 END 